/**
 * File:	clients/inst_firstboot.ycp
 * Package:	Configuration of firstboot
 * Summary:	Main file
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 */

{

textdomain "firstboot";
import "Firstboot";
import "Wizard";
import "Popup";

map display = UI::GetDisplayInfo();
integer space = display["TextMode"]:true ? 1 : 3;

string caption = _("License Agreement");

term contents = `VBox (
        `VSpacing (space),
        `HBox (
            `HSpacing (2*space),
            Firstboot::DetectRichText("FIRSTBOOT_LICENSE_FILE", ""),
            `HSpacing (2*space)
            ),
        `VSpacing (2),
        `RadioButtonGroup(`id(`eula),
            `HBox(
                `HSpacing (2*space),
            `VBox(
                `Left(`RadioButton(`id(`yes), _("Yes, I agree to the License Agreement"))),
                `Left( `RadioButton(`id(`no), _("No, I do not agree.")))
                ),
            `HSpacing (2*space)
                )
            ),
        `VSpacing(2)
        );


string help = _("<p>Read the License Agreement carefully and select
one of the available options. If you do not agree to the License Agreement,
the configuration will be aborted.</p>");

Wizard::SetContents(caption, contents, help, (boolean)WFM::Args (0),
                (boolean)WFM::Args (1));
Wizard::DisableAbortButton();
Wizard::SetFocusToNextButton();

any ret = nil;
while (true)
{
    ret = UI::UserInput();
    symbol accept = (symbol)UI::QueryWidget(`id(`eula), `CurrentButton);
    if (accept == `no)
    {
        ret = `abort;
    }
    else if ( accept != `yes && accept != `no )
    {
        Popup::Message(_("You have to either accept or decline the License
Agreement"));
        continue;
    }
    break;
}

return ret;

/* EOF */
}
